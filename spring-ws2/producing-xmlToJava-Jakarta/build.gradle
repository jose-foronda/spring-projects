plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.1'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

// tag::xsd[]
sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			srcDir 'build/generated-sources/jaxb'
		}
	}
}

task genJaxb {
	ext.sourcesDir = "src/main/java/"
	ext.schema = "src/main/resources/MISMO_3.4.0_B324.xsd"
	ext.bindings = "src/main/xjb/bindings.xml"
	outputs.dir sourcesDir

	doLast() {
		project.ant {
			taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
					classpath: configurations.jaxb.asPath
			mkdir(dir: sourcesDir)

			xjc(destdir: sourcesDir, schema: schema, binding: bindings) {
				produces(dir: sourcesDir, includes: "**/*.java")
			}
		}
	}
}

compileJava.dependsOn genJaxb
// end::xsd[]

// tag::jaxb[]
configurations {
	jaxb
}


bootJar {
	archiveBaseName = 'gs-producing-web-service'
	archiveVersion =  '0.1.0'
}
// end::jaxb[]

// tag::dependencies[]
dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-web-services'
	implementation 'wsdl4j:wsdl4j'
//	jaxb("com.sun.xml.bind:jaxb-xjc")
	jaxb "org.glassfish.jaxb:jaxb-xjc:4.0.4"
	jaxb "org.glassfish.jaxb:jaxb-runtime:4.0.4"
	jaxb "org.glassfish.jaxb:jaxb-core:4.0.4"
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
// end::dependencies[]

tasks.named('test') {
	useJUnitPlatform()
}
